import org.gradle.api.tasks.Exec

plugins {
    id 'com.android.library'
    //alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace 'uniffi.restsend_sdk'
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    api 'net.java.dev.jna:jna:5.16.0@aar'
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core   
}

def cargoTomlFile = layout.projectDirectory.file('../../crates/restsend/Cargo.toml')
def cargoVersionProvider = providers.fileContents(cargoTomlFile).asText.map { text ->
    def m = text =~ /version\s*=\s*"(.+?)"/
    if (m) m[0][1] else throw new GradleException("Version not found in Cargo.toml")
}

tasks.register('getCargoVersion') {
    inputs.file(cargoTomlFile)
    outputs.upToDateWhen { false }
    doLast {
        logger.lifecycle("Cargo.toml version: ${cargoVersionProvider.get()}")
    }
}

tasks.register('buildAndDeploy', Exec) {
    dependsOn('assemble', 'getCargoVersion')

    // lazily locate the release AAR
    def aarFile = layout.buildDirectory.file('outputs/aar/app-release.aar')

    inputs.file(aarFile)
    inputs.file(cargoTomlFile)

    doFirst {
        def resolvedAar = aarFile.get().asFile
        if (!resolvedAar.exists()) {
            throw new GradleException("AAR file not found: $resolvedAar")
        }
        def version = cargoVersionProvider.get()
        def targetPath = "ubuntu@chat.ruzhila.cn:/var/www/chat/downloads/restsend-sdk-${version}.aar"

        commandLine('rsync', '-az', resolvedAar.absolutePath, targetPath)
        println "Copying ${resolvedAar.absolutePath} to ${targetPath}"
    }
}
